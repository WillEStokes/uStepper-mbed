include(ExternalProject)

# set up GTest build
# --------------------------------------------------
set_property(DIRECTORY PROPERTY EP_BASE ${CMAKE_CURRENT_BINARY_DIR})

set(GTEST_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/Install/googletest) # ExternalProject creates this install dir

set(GTEST_LIB_PATH ${GTEST_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}gtest${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GTEST_MAIN_LIB_PATH ${GTEST_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}gtest_main${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GMOCK_LIB_PATH ${GTEST_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}gmock${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GMOCK_MAIN_LIB_PATH ${GTEST_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}gmock_main${CMAKE_STATIC_LIBRARY_SUFFIX})

ExternalProject_Add(googletest
	GIT_REPOSITORY    https://github.com/google/googletest.git
	GIT_TAG           release-1.8.1
	UPDATE_COMMAND ""
	CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR> -DBUILD_SHARED_LIBS=FALSE -DCMAKE_BUILD_TYPE=Release
	BUILD_BYPRODUCTS ${GTEST_LIB_PATH} ${GTEST_MAIN_LIB_PATH} ${GMOCK_LIB_PATH} ${GMOCK_MAIN_LIB_PATH}
	TEST_COMMAND      "")

# create imported targets
# --------------------------------------------------

# gtest links to system PThread when available
find_package(Threads)

# Fix error about include directory not existing
file(MAKE_DIRECTORY ${GTEST_INSTALL_DIR}/include)

# add library targets (matching format generated by FindGTest)
add_library(GTest::GTest STATIC IMPORTED GLOBAL)
set_property(TARGET GTest::GTest PROPERTY IMPORTED_LOCATION ${GTEST_LIB_PATH})
set_property(TARGET GTest::GTest PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INSTALL_DIR}/include)
set_property(TARGET GTest::GTest PROPERTY INTERFACE_LINK_LIBRARIES Threads::Threads)
add_dependencies(GTest::GTest googletest)

add_library(GTest::Main STATIC IMPORTED GLOBAL)
set_property(TARGET GTest::Main PROPERTY IMPORTED_LOCATION ${GTEST_MAIN_LIB_PATH})
set_property(TARGET GTest::Main PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INSTALL_DIR}/include)
set_property(TARGET GTest::Main PROPERTY INTERFACE_LINK_LIBRARIES GTest::GTest)
add_dependencies(GTest::Main googletest)

add_library(GMock::GMock STATIC IMPORTED GLOBAL)
set_property(TARGET GMock::GMock PROPERTY IMPORTED_LOCATION ${GMOCK_LIB_PATH})
set_property(TARGET GMock::GMock PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INSTALL_DIR}/include)
set_property(TARGET GMock::GMock PROPERTY INTERFACE_LINK_LIBRARIES GTest::GTest)
add_dependencies(GMock::GMock googletest)

# note: GMock main initializes both gmock and gtest, so use GMock::Main if both are being used in a project
add_library(GMock::Main STATIC IMPORTED GLOBAL)
set_property(TARGET GMock::Main PROPERTY IMPORTED_LOCATION ${GMOCK_MAIN_LIB_PATH})
set_property(TARGET GMock::Main PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INSTALL_DIR}/include)
set_property(TARGET GMock::Main PROPERTY INTERFACE_LINK_LIBRARIES GMock::GMock)
add_dependencies(GMock::Main googletest)